locals {
  s3_origin_id = "${local.resource_prefix}_${local.bucket_name}_a"
}

// https://stackoverflow.com/questions/51218979/react-router-doesnt-work-in-aws-s3-bucket/58978355#58978355
// This solves issue with LinkedIn https://www.linkedin.com/post-inspector/inspect
resource "aws_cloudfront_distribution" "s3_distribution_a" {
  origin {
    domain_name = local.s3_web_endpoint
    origin_id   = local.s3_origin_id

    // https://github.com/hashicorp/terraform-provider-aws/issues/7847
    custom_origin_config {
      http_port              = "80"
      https_port             = "443"
      origin_protocol_policy = "http-only"
      origin_ssl_protocols   = ["TLSv1", "TLSv1.1", "TLSv1.2"]
    }
  }

  // This handles react router redirect to index.html without returning 404 to viewer
  custom_error_response {
    error_code            = 404
    error_caching_min_ttl = 300
    response_code         = 200
    response_page_path    = "/index.html"
  }

  enabled         = true
  is_ipv6_enabled = true
  //  comment             = ""
  default_root_object = "index.html"

  logging_config {
    include_cookies = false
    bucket          = local.log_bucket_name
    prefix          = local.resource_prefix
  }

  aliases = [local.bucket_name, local.alt_name]

  default_cache_behavior {
    allowed_methods  = ["DELETE", "GET", "HEAD", "OPTIONS", "PATCH", "POST", "PUT"]
    cached_methods   = ["GET", "HEAD"]
    target_origin_id = local.s3_origin_id

    forwarded_values {
      query_string = false

      cookies {
        forward = "none"
      }
    }

    viewer_protocol_policy = "https-only"
    min_ttl                = 0
    default_ttl            = 3600
    max_ttl                = 86400
  }

  price_class = "PriceClass_All"

  restrictions {

    geo_restriction {
      restriction_type = "none"
    }
  }

  tags = merge(
    local.base_tags,
    {
      Name = "${local.resource_prefix}-${local.bucket_name}"
    },
  )

  viewer_certificate {
    acm_certificate_arn = local.cert_arn
    ssl_support_method  = "sni-only"
  }
}

// Create subdomain A record pointing to Cloudfront distribution
resource "aws_route53_record" "www" {
  zone_id = local.zone_id
  name    = "www.${local.bucket_name}"
  type    = "A"

  alias {
    name                   = aws_cloudfront_distribution.s3_distribution_a.domain_name
    zone_id                = aws_cloudfront_distribution.s3_distribution_a.hosted_zone_id
    evaluate_target_health = false
  }
}

// Create domain A record pointing to Cloudfront distribution
resource "aws_route53_record" "a" {
  zone_id = local.zone_id
  name    = local.bucket_name
  type    = "A"

  alias {
    name                   = aws_cloudfront_distribution.s3_distribution_a.domain_name
    zone_id                = aws_cloudfront_distribution.s3_distribution_a.hosted_zone_id
    evaluate_target_health = false
  }
}
